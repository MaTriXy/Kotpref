apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

group = groupName
version = versionName

def SITE_URL = 'https://github.com/chibatching/kotpref'
def GIT_URL = 'https://github.com/chibatching/kotpref.git'

tasks.named("dokkaHtml") {
    dokkaSourceSets {
        main {
            includes.from("README.md")
            sourceLink {
                localDirectory.set(file("src/main/kotlin"))
                remoteUrl.set(uri("https://github.com/chibatching/Kotpref/tree/v${version}/${project.name}/src/main/kotlin").toURL())
                remoteLineSuffix.set("#L")
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    signing {
        def signingKey = System.getenv("GPG_SIGNING_KEY")
        def signingPassword = System.getenv("GPG_SIGNING_PASS")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications
    }

    publishing {
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username System.getenv("SONATYPE_USER")
                    password System.getenv("SONATYPE_PASS")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release

                artifact javadocJar
                artifact sourcesJar

                pom {
                    artifactId archivesBaseName
                    name = moduleName
                    description = moduleDescription

                    packaging 'aar'
                    url = SITE_URL

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'chibatching'
                            name = 'Takao Chiba'
                            email = 'takao.chiba@gmail.com'
                        }
                    }
                    scm {
                        connection = GIT_URL
                        developerConnection = GIT_URL
                        url = SITE_URL
                    }
                }
            }
        }
    }
}
